name: Deploy on Windows
runs:
  using: composite
  steps:
    - name: Install environment
      shell: cmd
      run: |
        cd /d %USERPROFILE%

        echo Installing MSYS2
        C:\msys64\usr\bin\bash -lc "pacman -S --needed --noconfirm base-devel git tar pkg-config unzip p7zip zip autoconf autoconf-archive automake make patch gnupg"
        C:\msys64\usr\bin\bash -lc "pacman -S --needed --noconfirm mingw-w64-x86_64-nasm mingw-w64-x86_64-toolchain mingw-w64-x86_64-libtool mingw-w64-x86_64-gcc mingw-w64-i686-gcc mingw-w64-x86_64-gcc-fortran mingw-w64-i686-gcc-fortran mingw-w64-x86_64-libwinpthread-git mingw-w64-i686-libwinpthread-git mingw-w64-x86_64-SDL mingw-w64-i686-SDL mingw-w64-x86_64-ragel"
        set "PATH=C:\msys64\usr\bin;%PATH%"

        echo Installing Windows SDK 8.1
        curl -Lo sdksetup.exe https://go.microsoft.com/fwlink/p/?LinkId=323507
        sdksetup.exe /features OptionId.WindowsDesktopSoftwareDevelopmentKit OptionId.NetFxSoftwareDevelopmentKit /quiet

        echo Removing broken stuff from WSL and MSYS2
        rm "C:/WINDOWS/system32/bash.EXE" "C:/msys64/mingw64/bin/cmake.exe" "C:/msys64/usr/bin/link.exe" "C:/msys64/usr/bin/python.exe" "C:/msys64/usr/bin/python3.exe"

        python -m pip install gdown || python -m pip install gdown

        if not "%CI_DEPLOY_NEED_BAZEL%"=="" (
          echo Installing Bazel
          curl -L https://github.com/bazelbuild/bazel/releases/download/0.25.3/bazel-0.25.3-windows-x86_64.exe -o C:/msys64/mingw64/bin/bazel.exe

          echo Removing old versions of MSVC that interfere with Bazel
          bash -c "find 'C:/Program Files (x86)/Microsoft Visual Studio/2019/Enterprise/VC/' \( -name '14.1*' -or -name '14.2[01234567]*' \) -prune -exec rm -Rf {} \;"
          dir /s /a "C:\Program Files (x86)\Microsoft Visual Studio\2019\Enterprise\VC\14.*"
        )

        if not "%CI_DEPLOY_NEED_GCC%"=="" (
          echo Installing an older less buggy version of GCC
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-gcc-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-gcc-ada-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-gcc-objc-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-gcc-libs-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-gcc-fortran-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-gcc-libgfortran-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-binutils-2.35.1-3-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-crt-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-headers-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-libmangle-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-libwinpthread-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-tools-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-winpthreads-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/i686/mingw-w64-i686-winstorecompat-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-gcc-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-gcc-ada-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-gcc-objc-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-gcc-libs-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-gcc-fortran-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-gcc-libgfortran-10.2.0-5-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-binutils-2.35.1-3-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-crt-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-headers-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-libmangle-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-libwinpthread-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-tools-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-winpthreads-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          curl -LO http://repo.msys2.org/mingw/x86_64/mingw-w64-x86_64-winstorecompat-git-9.0.0.6029.ecb4ff54-1-any.pkg.tar.zst
          bash -c "pacman -Rc --noconfirm mingw-w64-i686-libgccjit mingw-w64-x86_64-libgccjit"
          bash -c "pacman -U --noconfirm *.pkg.tar.zst"
        )

        if "%CI_DEPLOY_PLATFORM%"=="windows-x86_64" if not "%CI_DEPLOY_NEED_CUDA%"=="" (
          echo Installing CUDA, cuDNN, etc
          curl -LO https://developer.download.nvidia.com/compute/cuda/11.2.2/local_installers/cuda_11.2.2_461.33_win10.exe
          curl -LO https://developer.download.nvidia.com/compute/redist/cudnn/v8.1.1/cudnn-11.2-windows-x64-v8.1.1.33.zip
          cuda_11.2.2_461.33_win10.exe -s
          unzip cudnn-11.2-windows-x64-v8.1.1.33.zip
          move cuda\bin\*.dll "%ProgramFiles%\NVIDIA GPU Computing Toolkit\CUDA\v11.2\bin"
          move cuda\include\*.h "%ProgramFiles%\NVIDIA GPU Computing Toolkit\CUDA\v11.2\include"
          move cuda\lib\x64\*.lib "%ProgramFiles%\NVIDIA GPU Computing Toolkit\CUDA\v11.2\lib\x64"
        )

        if "%CI_DEPLOY_MODULE%"=="nvcodec" (
          echo Installing NVIDIA Video Codec SDK
          python -m gdown.cli https://drive.google.com/uc?id=1cBohhcptNDSF5pxsQ7n4-qcGsgiWUxga
          unzip Video_Codec_SDK_11.0.10.zip
          move Video_Codec_SDK_11.0.10 "%ProgramFiles%\NVIDIA GPU Computing Toolkit\VideoCodecSDK"
        )

        if "%CI_DEPLOY_MODULE%"=="tensorrt" (
          echo Installing TensorRT
          python -m gdown.cli https://drive.google.com/uc?id=12r8CgDD66hIYB3WIX5JQix9zVeYVeHDR
          unzip TensorRT-7.2.3.4.Windows10.x86_64.cuda-11.2.cudnn8.1.zip
          move TensorRT-7.2.3.4 "%ProgramFiles%\NVIDIA GPU Computing Toolkit\TensorRT"
        )

        if "%CI_DEPLOY_MODULE%"=="mkl" (
          echo Installing MKL
          curl -LO https://registrationcenter-download.intel.com/akdlm/irc_nas/17754/w_onemkl_p_2021.2.0.296_offline.exe
          w_onemkl_p_2021.2.0.296_offline.exe -s -a -s --eula accept
        )

        if "%CI_DEPLOY_PLATFORM%"=="windows-x86" if "%CI_DEPLOY_MODULE%"=="flycapture" (
          echo Installing FlyCapture x86
          python -m gdown.cli https://drive.google.com/uc?id=1ctSSAMF5IkxTKWiiLtID-ltmm27pHFdr
          rem We can get this msi file by starting the installation from the exe file
          cmd.exe /c "msiexec /quiet /i FlyCapture_2.13.3.31_x86.msi ADDLOCAL=ALL"
        )

        if "%CI_DEPLOY_PLATFORM%"=="windows-x86_64" if "%CI_DEPLOY_MODULE%"=="flycapture" (
          echo Installing FlyCapture x64
          python -m gdown.cli https://drive.google.com/uc?id=14QM7W5RHhvZanF1UBobgEIvwdy6VwTht
          rem We can get this msi file by starting the installation from the exe file
          cmd.exe /c "msiexec /quiet /i FlyCapture_2.13.3.31_x64.msi ADDLOCAL=ALL"
        )

        if "%CI_DEPLOY_PLATFORM%"=="windows-x86" if "%CI_DEPLOY_MODULE%"=="spinnaker" (
          echo Installing Spinnaker x86
          python -m gdown.cli https://drive.google.com/uc?id=1kufOTKKEGXbaQemjRi8Zy2H98FkL-k6W
          python -m gdown.cli https://drive.google.com/uc?id=1FNweWcn-keLqvy56xqz3Ve_T3adz43rW
          rem We can get these msi files by starting the installation from the exe file
          cmd.exe /c "msiexec /quiet /i Spinnaker-1.27.0.48_Binaries_v140_x86.msi ADDLOCAL=ALL"
          cmd.exe /c "msiexec /quiet /i Spinnaker-1.27.0.48_SourceCode_v140_x86.msi ADDLOCAL=ALL"
        )

        if "%CI_DEPLOY_PLATFORM%"=="windows-x86_64" if "%CI_DEPLOY_MODULE%"=="spinnaker" (
          echo Installing Spinnaker x64
          python -m gdown.cli https://drive.google.com/uc?id=1xIqjBwl3Q9GnEPcs9JdZBJNmOSWD8gRE
          python -m gdown.cli https://drive.google.com/uc?id=1l74LjV8rANk_P_CvXZOMSzi7J02YVkfD
          rem We can get these msi files by starting the installation from the exe file
          cmd.exe /c "msiexec /quiet /i Spinnaker-1.27.0.48_Binaries_v140_x64.msi ADDLOCAL=ALL"
          cmd.exe /c "msiexec /quiet /i Spinnaker-1.27.0.48_SourceCode_v140_x64.msi ADDLOCAL=ALL"
        )

        set GRADLE_TASK=publishToMavenLocal
        set MAVEN_PHASE=install
        set "MESSAGE=${{ github.event.head_commit.message }}"
        if "%MESSAGE:~0,7%"=="Release" set DEPLOY=RELEASE
        if "%GITHUB_REF:~11%"=="release" set DEPLOY=RELEASE
        if not "%CI_DEPLOY_USERNAME%"=="" if not "%GITHUB_EVENT_NAME%"=="pull_request" (
          set GRADLE_TASK=publish
          set MAVEN_PHASE=deploy
          if "%DEPLOY%"=="RELEASE" (
            python -m gdown.cli -q -O settings.tar.gz https://drive.google.com/uc?id=%CI_DEPLOY_SETTINGS%
            bash -c "tar -xzf settings.tar.gz -C $HOME"
            set "MAVEN_OPTIONS=%MAVEN_OPTIONS% -DperformRelease -DstagingRepositoryId=%STAGING_REPOSITORY%"
          )
        )
        echo GPG_PASSPHRASE=*** >> %GITHUB_ENV%
        echo GRADLE_TASK=%GRADLE_TASK% >> %GITHUB_ENV%
        echo MAVEN_PHASE=%MAVEN_PHASE% >> %GITHUB_ENV%
        echo MAVEN_OPTIONS=%MAVEN_OPTIONS% >> %GITHUB_ENV%

    - name: Build project
      shell: cmd
      run: |
        if "%CI_DEPLOY_PLATFORM%"=="windows-x86_64" (
          call "%ProgramFiles(x86)%\Microsoft Visual Studio\2019\Enterprise\VC\Auxiliary\Build\vcvarsall.bat" amd64
          set MSYSTEM=MINGW64
        )
        if "%CI_DEPLOY_PLATFORM%"=="windows-x86" (
          call "%ProgramFiles(x86)%\Microsoft Visual Studio\2019\Enterprise\VC\Auxiliary\Build\vcvarsall.bat" x86
          set MSYSTEM=MINGW32
        )
        if not "%CI_DEPLOY_NEED_BAZEL%"=="" (
          echo Shorten work paths to prevent Bazel from reaching MAX_PATH limit
          set "TEST_TMPDIR=C:\tmp"
          set "TMPDIR=C:\tmp"
          set "TEMP=C:\tmp"
          set "TMP=C:\tmp"
          mkdir C:\tmp
          C:/msys64/mingw64/bin/bazel.exe version
        )
        if exist "%ProgramFiles%\NVIDIA GPU Computing Toolkit" (
          set "CUDA_PATH=%ProgramFiles%\NVIDIA GPU Computing Toolkit\CUDA\v11.2"
          set "CUDA_PATH_V11_2=%ProgramFiles%\NVIDIA GPU Computing Toolkit\CUDA\v11.2"
          set "PATH=%ProgramFiles%\NVIDIA GPU Computing Toolkit\CUDA\v11.2\bin;%ProgramFiles%\NVIDIA GPU Computing Toolkit\CUDA\v11.2\libnvvp;%PATH%"
          echo CUDA Version 11.2.182>"%ProgramFiles%\NVIDIA GPU Computing Toolkit\CUDA\v11.2\version.txt"
        )
        set "PATH=C:\msys64\%MSYSTEM%\bin;C:\msys64\usr\bin;%PATH%"

        bash --version
        git --version
        cl
        gcc --version
        cmake --version
        gradle --version
        call mvn -version
        gpg --version
        df -h
        wmic pagefile list /format:list

        set MAKEJ=%NUMBER_OF_PROCESSORS%
        echo Fetching %GITHUB_REPOSITORY%@%GITHUB_SHA%
        git init
        git fetch --depth 1 https://github.com/%GITHUB_REPOSITORY% %GITHUB_SHA%
        git checkout %GITHUB_SHA%
        git submodule update --init --recursive
        git submodule foreach --recursive "git reset --hard"

        if not "%CI_DEPLOY_NEED_GRADLE%"=="" (goto :gradle) else (goto :maven)

        :gradle
        echo Executing Gradle %GRADLE_TASK% ${{ matrix.options }} on %MAKEJ% processors
        set "GRADLE_OPTIONS=-Dorg.gradle.jvmargs=-Xmx2048m -PjavacppPlatform=%CI_DEPLOY_PLATFORM% -PjavacppPlatformExtension=${{ matrix.ext }} ${{ matrix.options }} --info"
        gradle clean build %GRADLE_OPTIONS% -b %CI_DEPLOY_MODULE%/build.gradle
        if ERRORLEVEL 1 exit /b
        echo Publishing only now to help avoid race conditions with Gradle
        gradle %GRADLE_TASK% %GRADLE_OPTIONS% -b %CI_DEPLOY_MODULE%/build.gradle --refresh-dependencies
        if ERRORLEVEL 1 exit /b
        goto :done3

        :maven
        mkdir %USERPROFILE%\.m2
        echo ^<settings^> > %USERPROFILE%\.m2\settings.xml
        echo ^<mirrors^>^<mirror^>^<id^>google-maven-central^</id^>^<name^>GCS Maven Central mirror^</name^>^<url^>https://maven-central.storage-download.googleapis.com/maven2/^</url^>^<mirrorOf^>central^</mirrorOf^>^</mirror^>^</mirrors^> >> %USERPROFILE%\.m2\settings.xml
        echo ^<servers^>^<server^>^<id^>sonatype-nexus-snapshots^</id^>^<username^>%CI_DEPLOY_USERNAME%^</username^>^<password^>%CI_DEPLOY_PASSWORD%^</password^>^</server^>^<server^>^<id^>sonatype-nexus-staging^</id^>^<username^>%CI_DEPLOY_USERNAME%^</username^>^<password^>%CI_DEPLOY_PASSWORD%^</password^>^</server^>^</servers^> >> %USERPROFILE%\.m2\settings.xml
        echo ^</settings^> >> %USERPROFILE%\.m2\settings.xml

        set /a "SECONDS=%RANDOM% %% 300"
        echo Sleeping %SECONDS% seconds to help avoid race conditions with Maven
        timeout %SECONDS% > nul

        echo Executing Maven %MAVEN_PHASE% on %MAKEJ% processors
        set EXT=${{ matrix.ext }}
        if "%EXT%" == "" (set EXT2=) else (set EXT2=%EXT:~1%)
        set "MAVEN_OPTS=-Xss2m"
        set "MAVEN_OPTIONS=clean %MAVEN_PHASE% -B -U -e -Dhttp.keepAlive=false -Dmaven.wagon.http.pool=false -Dmaven.wagon.http.retryHandler.count=3 -Djavacpp.platform=%CI_DEPLOY_PLATFORM% -Djavacpp.platform.extension=%EXT% %MAVEN_OPTIONS%"

        setlocal enabledelayedexpansion
        for /l %%i in (1,1,5) do (set STATUS=0 & call mvn %MAVEN_OPTIONS% -pl .,%CI_DEPLOY_MODULE% && goto :done || set STATUS=!ERRORLEVEL! && timeout 60 > nul)
        exit /b !STATUS!
        :done
        for /l %%i in (1,1,5) do (set STATUS=0 & call mvn %MAVEN_OPTIONS% -f %CI_DEPLOY_MODULE%/platform/%EXT2%/pom.xml && goto :done2 || set STATUS=!ERRORLEVEL! && timeout 60 > nul)
        exit /b !STATUS!
        :done2
        if exist %CI_DEPLOY_MODULE%/platform/redist/pom.xml (
          for /l %%i in (1,1,5) do (set STATUS=0 & call mvn %MAVEN_OPTIONS% -f %CI_DEPLOY_MODULE%/platform/redist/pom.xml && goto :done3 || set STATUS=!ERRORLEVEL! && timeout 60 > nul)
          exit /b !STATUS!
        )
        :done3
        df -h
        wmic pagefile list /format:list

    - name: Clean up
      shell: cmd
      run: |
        cd /d %USERPROFILE%
        set "PATH=C:\msys64\usr\bin;%PATH%"
        bash -c "rm -Rf $(find .m2/repository/ -name '*SNAPSHOT*')"
